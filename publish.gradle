def gitUrl = 'https://github.com/deviant-studio/RxContacts'
def libGroupId = group
def libVersion = version

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'RxContacts'
            description 'Reactive android contacts helper'
            url gitUrl
            packaging 'aar'
            groupId libGroupId
            version libVersion
            artifactId 'library'

            licenses {
                license {
                    name 'Apache-2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection gitUrl
                url gitUrl
            }
            developers {
                developer {
                    name 'Deviant Studio'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayApikey')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'rxcontacts'
        publish = true
        licenses = ["Apache-2.0"]
        vcsUrl = gitUrl
        version {
            name = libVersion
        }
    }
}

task publishToBintray

publishToBintray.dependsOn bintrayUpload
bintrayUpload.dependsOn install